$${

//  World Unscrambler				;
//  From lomexicano				;
//  Discord Tag: lomexicano#0001		;
//  Discord ID: 277301646050983936		;

// want to include a header saying how many letters the following words have? set this to true;
push_output_header_too = false;

// want to output only words with the same size as the one you entered? set this to true;
only_output_with_same_size = false;


// Modules required:
//	-> Klacaiba (by spthiel): 	https://spthiel.github.io/Modules/?name=Klacaiba
//  -> or HTTP (by xaero_):		https://spthiel.github.io/Modules/?name=HTTP


prompt(&input,"$$?","Type a word");
ifmatches("%&input%","^\w+$");
else;
	log("&f&l[&2&lMA&f&lC&4&lRO&f&l] &7''&e%&input%&7'' &cis not a valid word");
	stop;
endif;

unset(&http);

set(&api,"<h3>");

if(MODULENEI);
	&http = http("GET","https://wordunscrambler.me/unscramble/%&input%");
elseif(MODULEHTTP);
	&http = httpget("https://wordunscrambler.me/unscramble/%&input%",,);
else;
	log(" ");
	log("&f&l[&2&lMA&f&lC&4&lRO&f&l] &fIt looks like you &cdon't have any module to make http-requests&f!");
	stop;
endif;

ifmatches("%&http%","%&api%");
else;
	if(MODULENEI);
		&http = http("GET","https://wordfinderx.com/words-for/%&input%");
	elseif(MODULEHTTP);
		&http = httpget("https://wordfinderx.com/words-for/%&input%",,);
	endif;
	set(&api,"<span>");
endif;
unsafe(500);
	split("","%&input%",&input[]);
	arraysize(&input[],#size);
	split("%&api%","%&http%",&array1[]);
	unset(&array_output[]);
	foreach(&array1[],&useful_lines);
		ifmatches("%&useful_lines%","^(\d+ Letter Words.*)</div>",&log,1);
			match("%&log%","^((\d+) Letter Words)(.*)$",{&name,#size_now,&rest});
			if((!only_output_with_same_size)||((#size_now == #size)&&(only_output_with_same_size)));
				if(push_output_header_too);
					push(&array_output[],"%&name%");
				endif;
				do();
					ifmatches("%&rest%","data\-word=(\"|')\w+(\"|')");
						match("%&rest%","(.*)data\-word=(?:\"|')(\w+)(?:\"|')(.*)",{&before,&word,&next});
						set(&rest,"%&before%%&next%");
						push(&array_output[],"%&word%");
					else;
						break;
					endif;
				loop;
			endif;
		endif;
	next;
endunsafe;

log("&f&l[&2&lMA&f&lC&4&lRO&f&l] &eOutput for &a%&input%&e:");
foreach(&array_output[],&log);
	log("%&log%");
next;

}$$
